
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИспБригады = ПолучитьФункциональнуюОпцию("мз_ИспользоватьБригадыВместоИсполнителей");
	ИспСложныеСтатусы = ПолучитьФункциональнуюОпцию("мз_ИспользоватьСложныеСтатусы");
	ИспВидыЗадач = ПолучитьФункциональнуюОпцию("мз_ИспользоватьВидыЗадач");
	СтепеньСжатия = Константы.мз_СтепеньСжатияОписанияЗадачи.Получить();
	
	ФизЛицо = Пользователи.ТекущийПользователь().ФизическоеЛицо;
	
	Если Объект.Ссылка.Пустая() Тогда		
		Объект.Постановщик = ?(Объект.Постановщик.Пустая(),ФизЛицо,Объект.Постановщик);
		Объект.ДатаНачала = ТекущаяДата();
		Объект.Статус = Справочники.мз_СтатусыЗадач.Новая;
		Объект.Клиент = ?(Объект.Клиент.Пустая(),ФизЛицо.Клиент,Объект.Клиент);
		Элементы.ФорматированныйТекст.АктивизироватьПоУмолчанию = Истина;
		ЭтаФорма.Модифицированность = Истина;
		Элементы.ГрСменаСтатуса.Доступность = Ложь;
		Элементы.Статус.Доступность = Ложь;
	Иначе
		Элементы.ТекстСообщения.АктивизироватьПоУмолчанию = Истина;
		ИсторияПереписки = ПолучитьИсторияПереписки();
	КонецЕсли;
	
	Исполнитель = ФизЛицо.Исполнитель;
	Элементы.Клиент.Видимость = Исполнитель;
	Элементы.Постановщик.Доступность = Исполнитель;
	
	Если Объект.Постановщик = ФизЛицо или Пользователи.ЭтоПолноправныйПользователь() Тогда
		Элементы.ФорматированныйТекст.Доступность = Истина;
		Элементы.Заголовок.Доступность = Истина;
		Элементы.КоманднаяПанельРедактирования.Видимость = Истина;
	Иначе
		Элементы.ФорматированныйТекст.ТолькоПросмотр = Истина;
		Элементы.КоманднаяПанельРедактирования.Видимость = Ложь;
		Элементы.Заголовок.Видимость = Ложь;
		Элементы.ПодзадачиОписание.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	УстановитьЗаголовокФормы();	
	
	ПользовательИсполнитель();
	
	УстановитьОтборВУчастниках();
	
	ОбновитьВидимостьЭлементов();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Если ИспСложныеСтатусы Тогда
		ЗагрузитьСписокЗначенийСтатусов();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ФорматированныйТекст = ТекущийОбъект.СутьЗадачи.Получить();
	// СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	// СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ТекстHTML = "";
	Вложения = Новый Структура;
	ФорматированныйТекст.ПолучитьHTML(ТекстHTML, Вложения);
	ТекущийОбъект.СутьЗадачи = Новый ХранилищеЗначения(ФорматированныйТекст,Новый СжатиеДанных(СтепеньСжатия)); 
	ТекущийОбъект.СутьЗадачиТекст = СтроковыеФункцииКлиентСервер.ИзвлечьТекстИзHTML(ТекстHTML);
	ТекущийОбъект.ДополнительныеСвойства.Вставить("ПричинаСменыСтатуса", ПричинаСменыСтатуса);
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Если ЗакрыватьПослеЗаписи И Открыта() Тогда
		Закрыть();
	Иначе
		ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
		Элементы.ГрСменаСтатуса.Доступность = Истина;
		Элементы.Статус.Доступность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	ЗакрыватьПослеЗаписи = Истина;
	Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));
КонецПроцедуры

&НаКлиенте
Процедура ФЗаписать(Команда)
	Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры
&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры
&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения,Объект);
КонецПроцедуры
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаСервере
Процедура ОбновитьВидимостьЭлементов()
	
	Если ИспСложныеСтатусы Тогда
		Элементы.ГрСменаСтатуса.Видимость = Ложь;
	Иначе
		Элементы.ФормаОтложитьЗадачу.Доступность = НЕ (Объект.Статус = Справочники.мз_СтатусыЗадач.Отложенная ИЛИ Объект.Статус = Справочники.мз_СтатусыЗадач.Отмененная);
		Элементы.ФормаОтменитьЗадачу.Доступность = НЕ Объект.Статус = Справочники.мз_СтатусыЗадач.Отмененная;
		Элементы.ФормаСледующийСтатус.Заголовок = ПолучитьЗаголовокСтатуса();
		Элементы.Статус.Вид = ВидПоляФормы.ПолеНадписи;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьЗаголовокСтатуса() Экспорт
	
	Надпись = "";
	Если Объект.Статус = Справочники.мз_СтатусыЗадач.Новая Тогда
		Надпись = "Принять в работу";
	ИначеЕсли Объект.Статус = Справочники.мз_СтатусыЗадач.ВРаботе Тогда
		Надпись = "Выполненная";
	ИначеЕсли Объект.Статус = Справочники.мз_СтатусыЗадач.Выполненная
		ИЛИ Объект.Статус = Справочники.мз_СтатусыЗадач.Отложенная
		ИЛИ Объект.Статус = Справочники.мз_СтатусыЗадач.Отмененная Тогда
		Надпись = "Вернуть в работу";	
	КонецЕсли;	
	Возврат Надпись;	
	
КонецФункции

&НаСервере
Процедура ПользовательИсполнитель()
	
	ВидимостьЭлемента =  Объект.Исполнитель = Пользователи.ТекущийПользователь().ФизическоеЛицо ИЛИ Пользователи.ЭтоПолноправныйПользователь();
	Элементы.ГруппаОценка.Видимость = НЕ ВидимостьЭлемента;	
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборВУчастниках()
	
	НовыйМассив = Новый Массив();
	НовыйМассив.Добавить(Новый ПараметрВыбора("Отбор.Клиент", Объект.Клиент));
	НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);	
	Элементы.Участники.ПараметрыВыбора = НовыеПараметры;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсторияПерепискиДокументСформирован(Элемент)
	#Если НЕ МобильныйКлиент и НЕ ВебКлиент  Тогда
		ДокументПервогоБраузера = Элемент.Документ;
		ОкноПервогоБраузера     = ДокументПервогоБраузера.parentWindow; // IE
		Если ОкноПервогоБраузера = Неопределено Тогда
			ОкноПервогоБраузера = ДокументПервогоБраузера.defaultView; // Прочие браузеры
		КонецЕсли;
		ОкноПервогоБраузера.scroll(Элемент.Документ.body.firstChild.nextSibling.clientHeight);
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьСообщение(Команда)
	Если НЕ СокрЛП(ТекстСообщения) = "" Тогда		
		ДобавитьСообщение();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ДобавитьСообщение() Экспорт
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		ТекстОшибки = "Ведение переписки возможно только после записи документа.";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	РегистрыСведений.мз_ИсторияПереписки.ДобавитьСообщение(Объект.Ссылка, ТекстСообщения);
	ТекстСообщения = "";
	ИсторияПереписки = ПолучитьИсторияПереписки();
	
КонецПроцедуры

&НаСервере
Функция ПолучитьИсторияПереписки() Экспорт
	
	МассивСообщений = РегистрыСведений.мз_ИсторияПереписки.ПолучитьМассивСообщений(Объект.Ссылка);
	Возврат РегистрыСведений.мз_ИсторияПереписки.СформироватьИсториюПерепискиHTML(МассивСообщений);	
	
КонецФункции

&НаКлиенте
Процедура УчастникиПриИзменении(Элемент)
	УчастникиПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура УчастникиПриИзмененииНаСервере()
	Объект.Участники.Очистить();
	Для каждого стр из Участники Цикл
		Строка = Объект.Участники.Добавить();
		Строка.Участник = стр.Значение;
	КонецЦикла;
	СвернутьТабличнуюЧастьНаСервере("Участники","Участник");
	ЭтаФорма.Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ЗаголовокПриИзменении(Элемент)
	УстановитьЗаголовокФормы();
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовокФормы()
	ЭтаФорма.Заголовок = Объект.Заголовок;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ОбновитьПереписку" Тогда
		ИсторияПереписки = ПолучитьИсторияПереписки();
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура КлиентПриИзменении(Элемент)
	УстановитьОтборВУчастниках();
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСтрокуПодзадачи(Команда)
	НоваяСтрока = Объект.Подзадачи.Добавить(); 
	Элементы.Подзадачи.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор(); 
	Элементы.Подзадачи.ТекущийЭлемент = Элементы.ПодзадачиОписание;
КонецПроцедуры

&НаСервере
Процедура СвернутьТабличнуюЧастьНаСервере(ИмяТабличнойЧасти,ИмяСтолбца)
	ТЗ=Объект[ИмяТабличнойЧасти].Выгрузить();
	ТЗ.Свернуть(ИмяСтолбца);
	Объект[ИмяТабличнойЧасти].Загрузить(ТЗ);
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьЗадачу(Команда)	
	СменитьСтатусНаСервере(Истина);
	СпроситьПричинуИлиЗапистьПослеСменыСтатуса();
КонецПроцедуры

&НаКлиенте
Процедура СледующийСтатус(Команда)		
	СменитьСтатусНаСервере();
	СпроситьПричинуИлиЗапистьПослеСменыСтатуса();
КонецПроцедуры

&НаКлиенте
Процедура ОтложитьЗадачу(Команда)	
	СменитьСтатусНаСервере(,Истина);
	СпроситьПричинуИлиЗапистьПослеСменыСтатуса();
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаПричиныИзмененияСтатуса(Строка, Параметры) Экспорт
	Если НЕ Строка = Неопределено Тогда
		ПричинаСменыСтатуса = Строка;	
	КонецЕсли;
	ЗаписатьПослеСменыСтатуса();
КонецПроцедуры

&НаКлиенте
Процедура СпроситьПричинуИлиЗапистьПослеСменыСтатуса()
	Если СпрашиватьПричинуСменыСтатуса() Тогда
		Оповещение = Новый ОписаниеОповещения("ПослеВводаПричиныИзмененияСтатуса",ЭтаФорма);
		ПоказатьВводСтроки(Оповещение, "", "Причина изменения статуса", 0, Истина);	
	Иначе
		ЗаписатьПослеСменыСтатуса();
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьПослеСменыСтатуса()
	ЭтаФорма.Записать();
	ПричинаСменыСтатуса = "";			
КонецПроцедуры	

&НаСервере
Процедура СменитьСтатусНаСервере(Отменить = Ложь,Отложить = Ложь)
	Если Отменить Тогда
		Объект.Статус = Справочники.мз_СтатусыЗадач.Отмененная;
	ИначеЕсли Отложить Тогда
		Объект.Статус = Справочники.мз_СтатусыЗадач.Отложенная;
	Иначе	
		Если Объект.Статус = Справочники.мз_СтатусыЗадач.Новая 
			ИЛИ Объект.Статус = Справочники.мз_СтатусыЗадач.Отложенная
			ИЛИ Объект.Статус = Справочники.мз_СтатусыЗадач.Отмененная Тогда
			Объект.Статус = Справочники.мз_СтатусыЗадач.ВРаботе;
		ИначеЕсли Объект.Статус = Справочники.мз_СтатусыЗадач.ВРаботе Тогда
			Объект.Статус = Справочники.мз_СтатусыЗадач.Выполненная;
		ИначеЕсли Объект.Статус = Справочники.мз_СтатусыЗадач.Выполненная Тогда
			Объект.Статус = Справочники.мз_СтатусыЗадач.ВРаботе;	
		КонецЕсли;	
	КонецЕсли; 
	ОбновитьВидимостьЭлементов();
КонецПроцедуры

&НаСервере
Функция СпрашиватьПричинуСменыСтатуса() Экспорт		
	Возврат Объект.Статус.СпрашиватьПричинуИзменения;	
КонецФункции 

&НаКлиенте
Процедура ИсполнительПриИзменении(Элемент)
	ПользовательИсполнитель();
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьСписокЗначенийСтатусов()
	Запрос = Новый запрос("ВЫБРАТЬ
	|	мз_СтатусыЗадач.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.мз_СтатусыЗадач КАК мз_СтатусыЗадач
	|ГДЕ
	|	мз_СтатусыЗадач.ПометкаУдаления = ЛОЖЬ
	|
	|УПОРЯДОЧИТЬ ПО
	|	мз_СтатусыЗадач.ИндексСтатуса");
	Таб = Запрос.Выполнить().Выгрузить();
	Элементы.Статус.СписокВыбора.ЗагрузитьЗначения(Таб.ВыгрузитьКолонку("Ссылка"));
КонецПроцедуры
