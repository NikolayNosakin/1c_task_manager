
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("Пользователь") Тогда
		Объект = Параметры.Пользователь;
	Иначе
		Объект = Пользователи.ТекущийПользователь();
	КонецЕсли;	
	
	ЗаполнитьДерево();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектПриИзменении(Элемент)
	ЗаполнитьДерево();
	РазвернутьВсеДерево();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДерево(Копирование = Ложь,ПользовательИсточникКопирования = Неопределено)
	
	ДеревоНастроек = РеквизитФормыВЗначение("ДеревоНастроек_");
	ДеревоНастроек.Строки.Очистить();
	Если Объект.Пустая() Тогда
		ЗначениеВРеквизитФормы(ДеревоНастроек,"ДеревоНастроек_");
		Возврат;
	КонецЕсли;	
	Запрос = Новый Запрос;
	Запрос.Текст ="ВЫБРАТЬ
	|	ПраваИНастройки.Ссылка,
	|	НастройкиПользователей.Значение КАК Значение,
	|	ПраваИНастройки.ЭтоГруппа КАК ЭтоГруппа,
	|	ПраваИНастройки.Родитель
	|ИЗ
	|	ПланВидовХарактеристик.ПраваИНастройки КАК ПраваИНастройки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиПользователей КАК НастройкиПользователей
	|		ПО ПраваИНастройки.Ссылка = НастройкиПользователей.Настройка
	|			И (НастройкиПользователей.Объект = &Объект)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЭтоГруппа ИЕРАРХИЯ,
	|	ПраваИНастройки.Наименование";
	ЗАпрос.УстановитьПараметр("Объект", ?(Копирование, ПользовательИсточникКопирования,Объект));
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СтрокаНастройки = ДеревоНастроек.Строки.Найти(Выборка.Ссылка, "Настройка", Истина);
		Если СтрокаНастройки = Неопределено Тогда
			Если НЕ Выборка.Родитель.Пустая() Тогда
				// Найдем группу настройки, если ее нет, то создадим
				СтрокаГруппы = ДеревоНастроек.Строки.Найти(Выборка.Родитель, "Настройка", Истина);
				Если СтрокаГруппы=Неопределено Тогда
					СтрокаГруппы = ДеревоНастроек.Строки.Добавить();
					СтрокаГруппы.Настройка = Выборка.Родитель;
					СтрокаГруппы.ЭтоНастройка = Ложь;
				КонецЕсли;
			Иначе
				СтрокаГруппы = ДеревоНастроек;
			КонецЕсли;		
			
			СтрокаНастройки = СтрокаГруппы.Строки.Добавить();
			СтрокаНастройки.Настройка = Выборка.Ссылка;
			СтрокаНастройки.ЭтоНастройка = НЕ Выборка.ЭтоГруппа;
		КонецЕсли;
		
		ЗначениеНастройки = Выборка.Ссылка.ТипЗначения.ПривестиЗначение(Выборка.Значение);
		
		СтрокаНастройки.Значение = ЗначениеНастройки;
		
	КонецЦикла;
	ЗначениеВРеквизитФормы(ДеревоНастроек,"ДеревоНастроек_");
	
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьВсеДерево()
	Для каждого Ид Из ДеревоНастроек_.ПолучитьЭлементы() Цикл
		Элементы.ДеревоНастроек_.Развернуть(Ид.ПолучитьИдентификатор(), Истина);
	КонецЦикла;    
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	ЗаписатьНастройки();
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНастройки()
	
	Если НЕ ЗначениеЗаполнено(Объект) Тогда
		Возврат;
	КонецЕсли;
	
	Набор = РегистрыСведений.НастройкиПользователей.СоздатьНаборЗаписей();
	Набор.Отбор.Объект.Использование = Истина;
	Набор.Отбор.Объект.Значение = Объект;
	ДеревоНастроек = РеквизитФормыВЗначение("ДеревоНастроек_");
	ЗаполнитьНаборЗаписей(ДеревоНастроек.Строки, Набор);
	
	Набор.Записать();
	
КонецПроцедуры

Процедура ЗаполнитьНаборЗаписей(СтрокиДерева, НаборЗаписей)
	
	Для Каждого СтрокаДерева Из СтрокиДерева Цикл
		
		Если СтрокаДерева.ЭтоНастройка Тогда
			Запись = НаборЗаписей.Добавить();
			Запись.Объект = Объект;
			Запись.Настройка = СтрокаДерева.Настройка;
			Запись.Значение = СтрокаДерева.Настройка.ТипЗначения.ПривестиЗначение(СтрокаДерева.Значение);
		Иначе
			ЗаполнитьНаборЗаписей(СтрокаДерева.Строки, НаборЗаписей);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СкопироватьНастройкиДругогоПользователя(Команда)
	ОткрытьФорму("Справочник.Пользователи.Форма.ФормаСписка",Новый Структура("РежимВыбора",Истина),ЭтаФорма,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ВыбранноеЗначение.пустая() Тогда
		Возврат;
	КонецЕсли;	
	ЗаполнитьДерево(Истина,ВыбранноеЗначение);
	РазвернутьВсеДерево();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьРегистрНастроек(Команда)
	ОткрытьФорму("РегистрСведений.НастройкиПользователей.Форма.ФормаСписка");
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписокНастроек(Команда)
	ОткрытьФорму("ПланВидовХарактеристик.ПраваИНастройки.ФормаСписка");
КонецПроцедуры
